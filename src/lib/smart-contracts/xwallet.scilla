(* v0.9.5
DIDxWallet.tyron: decentralized identifier smart wallet.
Self-Sovereign Identity Protocol.
Copyright (C) Tyron Pungtas and its affiliates.
www.ssiprotocol.com

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.*)

scilla_version 0

import PairUtils BoolUtils

library DIDxWallet

  (* The DID library *)

  type DIDstatus =
    | Deployed
    | Created
    | Updated
    | Recovered
    | Deactivated
    | Locked

  type Operation =
    | Recovery
    | Update
  
  type Action =
    | Add
    | Remove

  type TransferProtocol =
    | Https
    | Git

  type Endpoint =
    | Address of ByStr20
    | Uri of String TransferProtocol String   (* type, transfer protocol & uri *)

  type Document =
    | VerificationMethod of Action String ByStr33  (* string: key purpose OR service ID *)
    | Service of Action String Endpoint
  
  type Recoverer =
    | First
    | Second
  
  type Beneficiary =
    | UserDomain of String String
    | BeneficiaryAddr of ByStr20
    
  let didRecovery = Recovery
  let didUpdate = Update
  let update = "update"
  let recovery = "recovery"
  let didsr = "socialrecovery"
  let actionAdd = "add"
  let actionRemove = "remove"
  let empty_methods = Emp String ByStr33
  let empty_services = Emp String ByStr20
  let empty_services_ = Emp String Endpoint
  
  let one_msg = fun( msg: Message ) =>
    let nil_msg = Nil{ Message } in Cons{ Message } msg nil_msg

  let two_msgs = fun( msg1: Message ) => fun( msg2: Message ) =>
    let msgs_tmp = one_msg msg2 in Cons{ Message } msg1 msgs_tmp

  type Error =
    | CodeWrongStatus
    | CodeNotAdmin
    | CodeWrongCaller
    | CodeWrongSignature
    | CodeUndefinedKey
    | CodeSameKey
    | CodeSameId
    | CodeNotValid
    | CodeDidLocked
 
  let make_error = fun( error: Error ) =>
    let result = match error with
    | CodeWrongStatus            => Int32 -1
    | CodeNotAdmin               => Int32 -2
    | CodeWrongCaller            => Int32 -3
    | CodeWrongSignature         => Int32 -4
    | CodeUndefinedKey           => Int32 -5
    | CodeSameKey                => Int32 -6
    | CodeSameId                 => Int32 -8
    | CodeNotValid               => Int32 -9
    | CodeDidLocked              => Int32 -10
    end in { _exception: "Error"; code: result }
  
  let zero = Uint128 0
  let zeroByStr20 = 0x0000000000000000000000000000000000000000
  let zeroByStr33 = 0x000000000000000000000000000000000000000000000000000000000000000000
  let zeroByStr64 = 0x00000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000
  let zero_bystr = builtin to_bystr zeroByStr20

  let option_value = tfun 'A => fun( default: 'A ) => fun( input: Option 'A) =>
    match input with
    | Some v => v
    | None => default end
  let option_uint128_value = let f = @option_value Uint128 in f zero
  let option_bystr20_value = let f = @option_value ByStr20 in f zeroByStr20
  let option_bystr33_value = let f = @option_value ByStr33 in f zeroByStr33
  let option_bystr64_value = let f = @option_value ByStr64 in f zeroByStr64
  
  (* The xWallet library *)
  
  type Data =
  | Data of String String String Uint128 Uint128 Uint128   (* id, address name, second address name, amount, second amount, third amount *)
  
  let domain_ = "did"
  let education = "education"
  let crd = "crd"
  let stakezil = "stakezil"
  let zilswap = "zilswap"
  let xorder = "xorder"
  let fifteen_min = Uint128 21
  let one_hundred = Uint128 100
  let fixed = None{ Uint128 }

contract DIDxWallet(
  initAdmin: ByStr20,
  init_tyron: ByStr20 with contract field services_: Map String ByStr20, field dns: Map String ( Map String ByStr20 ), field psc_fee: Map String Uint128 end
  )
  field admin_: ByStr20 = initAdmin
  field did_: String = ""    (* the decentralized identifier *)
  field did_status_: DIDstatus = Deployed
  
  field init_: ByStr20 with contract field services_: Map String ByStr20, field dns: Map String ( Map String ByStr20 ), field psc_fee: Map String Uint128 end = init_tyron
  field agent: ByStr20 = initAdmin

  (* Verification methods @key: key purpose @value: public key of type "SchnorrSecp256k1VerificationKey2019" *)
  field verification_methods_: Map String ByStr33 = empty_methods
  
  (* Services @key: ID @value: endpoint *)
  field services_: Map String ByStr20 = empty_services
  field services__: Map String Endpoint = empty_services_
  field service_addr: Map String ByStr20 = Emp String ByStr20
  
  field document_hash: ByStr = zero_bystr
  
  field social_recovery_: Option( Pair ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end )
    = None{( Pair ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end )}
  
  (* The block number when the DID Create operation occurred *)
  field did_created_: BNum = BNum 0
  
  (* The block number when the last DID CRUD operation occurred *)  
  field ledger_time_: BNum = BNum 0
  
  (* A monotonically increasing number representing the amount of DID CRUD transactions that have taken place *)
  field crud_tx_number_: Uint128 = zero
  
  field deadline_block: Uint128 = fifteen_min
  
(* The DID backbone *)

procedure ThrowError( err: Error )
  e = make_error err; throw e end

procedure IsOperational()
  did_status <- did_status_;
  match did_status with
  | Deactivated => err = CodeWrongStatus; ThrowError err
  | Locked => err = CodeDidLocked; ThrowError err
  | _ => end end

procedure VerifyAdmin()
  current_admin <- admin_;
  verified = builtin eq current_admin _sender;
  match verified with
  | True =>
  | False => err= CodeNotAdmin; ThrowError err end end

procedure Payment(
  id: String,
  amount: Option Uint128
  )
  init <- init_;
  get_fee <-& init.psc_fee[id]; fee = option_uint128_value get_fee;
  is_zero = builtin eq fee zero;
  match is_zero with
  | True => err = CodeNotValid; ThrowError err
  | False => 
      match amount with
      | None => accept; msg = let m = { _tag: "AddFunds"; _recipient: init; _amount: fee; id: id } in one_msg m; send msg
      | Some amt => 
          fee_ = let percent = builtin div amt one_hundred in builtin mul fee percent;
          accept; msg = let m = { _tag: "AddFunds"; _recipient: init; _amount: fee_; id: id } in one_msg m; send msg end end end

transition UpdateAdmin( addr: ByStr20 )
  IsOperational; VerifyAdmin; Payment update fixed; admin_ := addr end

transition UpdateInit(
  addr: ByStr20 with contract field services_: Map String ByStr20, field dns: Map String ( Map String ByStr20 with contract field verification_methods_: Map String ByStr33 end ), field psc_fee: Map String Uint128 end
  )
  IsOperational; VerifyAdmin; Payment update fixed; init_ := addr end

transition UpdateAgent( addrName: String )
  IsOperational; VerifyAdmin; Payment update fixed;
  init <- init_; agent_ = "agent";
  get_addr <-& init.dns[agent_][addrName]; addr = option_bystr20_value get_addr;
  agent := addr end

transition EnableSocialRecovery(
  addr1: ByStr20 with contract field verification_methods_: Map String ByStr33 end,
  addr2: ByStr20 with contract field verification_methods_: Map String ByStr33 end
  )
  IsOperational; VerifyAdmin; Payment update fixed;
  recoverers = let sr = Pair{ ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end } addr1 addr2
    in Some{( Pair ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end )} sr;
  social_recovery_ := recoverers end

transition UpdateSocialRecoverer(
  recoverer: Recoverer,
  addr: ByStr20 with contract field verification_methods_: Map String ByStr33 end
  )
  IsOperational; VerifyAdmin; Payment update fixed;
  social_recovery <- social_recovery_;
  match social_recovery with
  | Some recoverers =>
      match recoverer with
      | First =>
          sr2addr = let snd_element = @snd ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end in snd_element recoverers;
          updated_sr = let sr = Pair{ ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end } addr sr2addr
           in Some{( Pair ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end )} sr;
          social_recovery_ := updated_sr
      | Second =>
          sr1addr = let fst_element = @fst ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end in fst_element recoverers;
          updated_sr = let sr = Pair{ ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end } sr1addr addr
           in Some{( Pair ByStr20 with contract field verification_methods_: Map String ByStr33 end ByStr20 with contract field verification_methods_: Map String ByStr33 end )} sr;
          social_recovery_ := updated_sr end
  | None => err = CodeNotValid; ThrowError err end end
  
transition Lock()
IsOperational; VerifyAdmin; Payment update fixed; 
  social_recovery <- social_recovery_;
  match social_recovery with
  | Some recoverers =>
  | None => err = CodeNotValid; ThrowError err end;
  locked = Locked; did_status_ := locked end
  
transition SocialRecovery( 
  addr: ByStr20 with contract field verification_methods_: Map String ByStr33 end,
  signature1: ByStr64,
  signature2: ByStr64
  )
  did_status <- did_status_;
  match did_status with
  | Deactivated => err = CodeWrongStatus; ThrowError err
  | _ => end;
  Payment didsr fixed;
  signed_data = builtin to_bystr addr;
  social_recovery <- social_recovery_;
  match social_recovery with
  | Some recoverers =>
      recoverer1addr = let fst_element = @fst
        ByStr20 with contract field verification_methods_: Map String ByStr33 end
        ByStr20 with contract field verification_methods_: Map String ByStr33 end
      in fst_element recoverers;
      recoverer2addr = let snd_element = @snd
        ByStr20 with contract field verification_methods_: Map String ByStr33 end
        ByStr20 with contract field verification_methods_: Map String ByStr33 end
      in snd_element recoverers;
      is_recoverer = let is_recoverer1 = builtin eq recoverer1addr _sender in let is_recoverer2 = builtin eq recoverer2addr _sender in orb is_recoverer1 is_recoverer2;
      match is_recoverer with
      | True =>
      | False => err = CodeWrongCaller; ThrowError err end;
      get_recoverer1key <-& recoverer1addr.verification_methods_[didsr]; did_sr1key = option_bystr33_value get_recoverer1key;
      is_right_signature1 = builtin schnorr_verify did_sr1key signed_data signature1;
      match is_right_signature1 with
      | True =>
          get_recoverer2key <-& recoverer2addr.verification_methods_[didsr]; did_sr2key = option_bystr33_value get_recoverer2key;
          is_right_signature2 = builtin schnorr_verify did_sr2key signed_data signature2;
          match is_right_signature2 with
          | True =>
          | False => err = CodeWrongSignature; ThrowError err end
      | False => err = CodeWrongSignature; ThrowError err end
  | None => err = CodeWrongStatus; ThrowError err end;
  admin_ := addr end

transition BuyDomainNameNFT( username: String )
  IsOperational; VerifyAdmin; init <- init_;
  get_fee <-& init.psc_fee[domain_]; fee = option_uint128_value get_fee;
  accept; msg = let m = { _tag: "BuyDomainNameNFT"; _recipient: init; _amount: fee;
    username: username;
    domain: domain_ } in one_msg m; send msg end
      
transition TransferDomainNameNFT(
  username: String,
  newAddr: ByStr20
  )
  IsOperational; VerifyAdmin; init <- init_;
  get_fee <-& init.psc_fee[domain_]; fee = option_uint128_value get_fee;
  accept; msg = let m = { _tag: "TransferDomainNameNFT"; _recipient: init; _amount: fee;
    username: username;
    domain: domain_;
    newAddr: newAddr } in one_msg m; send msg end

(* Verify Schnorr signature - signed data must correspond with a DID Key *)
procedure VerifySignature(
  id: String,
  signedData: ByStr,
  signature: ByStr64
  )
  get_did_key <- verification_methods_[id];
  is_right_signature = let did_key = option_bystr33_value get_did_key in builtin schnorr_verify did_key signedData signature;
  match is_right_signature with
    | True =>
    | False => err = CodeWrongSignature; ThrowError err end end

procedure ThrowIfNoKey( optKey: Option ByStr33 )
  match optKey with
  | Some key =>
  | None => err = CodeUndefinedKey; ThrowError err end end

procedure ThrowIfSameKey(
  key: ByStr33,
  sndKey: ByStr33
  )
  is_same_key = builtin eq key sndKey;
  match is_same_key with
  | True => err= CodeSameKey; ThrowError err
  | False => end end

procedure VerifyDIDkeys(
  operation: Operation,
  didRecovery: ByStr33,
  didUpdate: ByStr33
  )
  get_update_key <- verification_methods_[update]; new_update = option_bystr33_value get_update_key;
  match operation with
  | Recovery =>
      get_recovery_key <- verification_methods_[recovery]; new_recovery = option_bystr33_value get_recovery_key;
      ThrowIfSameKey new_recovery new_update;
      ThrowIfSameKey new_recovery didRecovery; ThrowIfSameKey new_recovery didUpdate; ThrowIfSameKey new_update didRecovery; ThrowIfSameKey new_update didUpdate
  | Update => ThrowIfSameKey new_update didUpdate; ThrowIfSameKey new_update didRecovery end end

procedure Timestamp()
  current_block <- &BLOCKNUMBER; ledger_time_ := current_block;
  latest_tx_number <- crud_tx_number_;
  new_tx_number = let incrementor = Uint128 1 in builtin add latest_tx_number incrementor; crud_tx_number_ := new_tx_number end

procedure SaveDocument( document: Document )
  match document with
  | VerificationMethod action purpose key =>
      match action with
      | Add => verification_methods_[purpose] := key
      | Remove => err = CodeNotValid; ThrowError err end
  | Service action id endpoint =>
      match action with
      | Add =>
          match endpoint with
          | Address addr => services_[id] := addr
          | Uri eType protocol uri => services__[id] := endpoint end
      | Remove => err = CodeNotValid; ThrowError err end end end

transition DidCreate(
  document: List Document,
  signature: Option ByStr64
  )
  did_status <- did_status_;
  match did_status with
  | Deployed =>
    VerifyAdmin; Payment crd fixed;
    new_did = let did_prefix = "did:tyron:zil:main:" in let did_suffix = builtin to_string _this_address 
      in builtin concat did_prefix did_suffix; did_ := new_did;
    forall document SaveDocument;
    get_recovery_key <- verification_methods_[recovery]; ThrowIfNoKey get_recovery_key; did_recovery = option_bystr33_value get_recovery_key;
    get_update_key <- verification_methods_[update]; ThrowIfNoKey get_update_key; did_update = option_bystr33_value get_update_key;
    ThrowIfSameKey did_recovery did_update;
    new_status = Created; did_status_ := new_status;
    current_block <- &BLOCKNUMBER; did_created_ := current_block; Timestamp
  | _ => err = CodeWrongStatus; ThrowError err end end

procedure UpdateDocument( document: Document )
  match document with
  | VerificationMethod action purpose key =>
      key_exists <- exists verification_methods_[purpose];
      match action with
      | Add =>
          match key_exists with
          | True =>
              is_update = builtin eq purpose update;
              match is_update with
              | True => verification_methods_[update] := key
              | False => err = CodeSameId; ThrowError err end
          | False => verification_methods_[purpose] := key end
      | Remove =>
          match key_exists with
          | True => delete verification_methods_[purpose]
          | False => err = CodeNotValid; ThrowError err end end
  | Service action id endpoint =>
      is_service_ <- exists services_[id];
      is_service__ <- exists services__[id];
      service_exists = orb is_service_ is_service__;
      match action with
      | Add =>
          match service_exists with
          | True => err = CodeSameId; ThrowError err
          | False => 
              match endpoint with
              | Address addr => services_[id] := addr
              | Uri eType protocol uri => services__[id] := endpoint end end
      | Remove =>
          match service_exists with
          | True => delete services_[id]; delete services__[id]
          | False => err = CodeNotValid; ThrowError err end end end end

procedure ValidateDocument(
  operation: Operation,
  document: List Document
  )
  match operation with
  | Recovery => 
      verification_methods_ := empty_methods; services_ := empty_services; services__ := empty_services_;
      forall document SaveDocument
  | Update => forall document UpdateDocument end end

procedure HashDocument( document: Document )
  doc_hash <- document_hash;
  match document with
  | VerificationMethod action purpose key =>
      match action with
      | Add =>
          hash = let h1 = builtin sha256hash actionAdd 
            in let h2 = builtin sha256hash purpose
            in let h3 = builtin sha256hash key
            in let h1_2 = builtin concat h1 h2
            in builtin concat h1_2 h3;
          doc_hash_ = let hash_ = builtin to_bystr hash in builtin concat doc_hash hash_;
          document_hash := doc_hash_
      | Remove =>
          hash = let h1 = builtin sha256hash actionRemove
            in let h2 = builtin sha256hash purpose
            in builtin concat h1 h2;
          doc_hash_ = let hash_ = builtin to_bystr hash in builtin concat doc_hash hash_;
          document_hash := doc_hash_ end
  | Service action id endpoint =>
      match action with
      | Add =>
          match endpoint with
          | Uri eType transfer uri =>
              hash = let h1 = builtin sha256hash actionAdd
              in let h2 = builtin sha256hash id
              in let h3 = builtin sha256hash uri
              in let h1_2 = builtin concat h1 h2
              in builtin concat h1_2 h3;
              doc_hash_ = let hash_ = builtin to_bystr hash in builtin concat doc_hash hash_;
              document_hash := doc_hash_
          | Address addr =>
              hash = let h1 = builtin sha256hash actionAdd
              in let h2 = builtin sha256hash id
              in let h3 = builtin sha256hash addr
              in let h1_2 = builtin concat h1 h2
              in builtin concat h1_2 h3;
              doc_hash_ = let hash_ = builtin to_bystr hash in builtin concat doc_hash hash_;
              document_hash := doc_hash_ end
      | Remove =>
          hash = let h1 = builtin sha256hash actionRemove
          in let h2 = builtin sha256hash id
          in builtin concat h1 h2;
          doc_hash_ = let hash_ = builtin to_bystr hash in builtin concat doc_hash hash_;
          document_hash := doc_hash_ end end end

procedure VerifyDocument(
  operation: Operation,
  document: List Document,
  signature: Option ByStr64
  )
  document_hash := zero_bystr;
  forall document HashDocument;
  doc_hash <- document_hash;
  sig = option_bystr64_value signature;
  id = match operation with
  | Recovery => recovery
  | Update => update end;
  VerifySignature id doc_hash sig;
  ValidateDocument operation document end

transition DidRecover(
  document: List Document,
  signature: Option ByStr64
  )
  did_status <- did_status_;
  match did_status with
  | Created => | Recovered => | Updated =>
  | _ => err = CodeWrongStatus; ThrowError err end;
  VerifyAdmin; Payment crd fixed;
  get_recovery_key <- verification_methods_[recovery]; did_recovery = option_bystr33_value get_recovery_key;
  get_update_key <- verification_methods_[update]; did_update = option_bystr33_value get_update_key;
  VerifyDocument didRecovery document signature;
  VerifyDIDkeys didRecovery did_recovery did_update;
  new_status = Recovered; did_status_ := new_status; Timestamp end

transition DidUpdate(
  document: List Document,
  signature: Option ByStr64
  )
  did_status <- did_status_;
  match did_status with
  | Created => | Recovered => | Updated =>
  | _ => err = CodeWrongStatus; ThrowError err end;
  VerifyAdmin; Payment update fixed;
  get_recovery_key <- verification_methods_[recovery]; did_recovery = option_bystr33_value get_recovery_key;
  get_update_key <- verification_methods_[update]; did_update = option_bystr33_value get_update_key;
  VerifyDocument didUpdate document signature;
  VerifyDIDkeys didUpdate did_recovery did_update;
  new_status = Updated; did_status_ := new_status; Timestamp end

transition DidDeactivate(
  document: List Document,
  signature: Option ByStr64
  ) 
  did_status <- did_status_;
  match did_status with
  | Created => | Recovered => | Updated =>
  | _ => err = CodeWrongStatus; ThrowError err end;
  VerifyAdmin; Payment crd fixed;
  VerifyDocument didRecovery document signature;
  verification_methods_ := empty_methods; services_ := empty_services; services__ := empty_services_;
  new_status = Deactivated; did_status_ := new_status; Timestamp end

(* Receive $ZIL native funds *)
transition AddFunds()
  IsOperational; Payment education fixed; accept end

(* Send $ZIL to any recipient that implements the tag, e.g. "AddFunds", "", etc. *)
transition SendFunds(
  tag: String,
  beneficiary: Beneficiary
  )
  IsOperational; VerifyAdmin; Payment education fixed;
  match beneficiary with
  | UserDomain username domain =>
      init <- init_;
      get_addr <-& init.dns[domain][username]; addr = option_bystr20_value get_addr;
      accept; msg = let m = { _tag: tag; _recipient: addr; _amount: _amount } in one_msg m; send msg
  | BeneficiaryAddr addr =>
      accept; msg = let m = { _tag: tag; _recipient: addr; _amount: _amount } in one_msg m; send msg end end

procedure FetchServiceAddr( id: String )
  init <- init_; get_service <-& init.services_[id]; addr = option_bystr20_value get_service; service_addr[id] := addr end

transition Transfer(
  addrName: String,
  beneficiary: Beneficiary,
  amount: Uint128
  ) 
  IsOperational; VerifyAdmin; Payment education fixed;
  FetchServiceAddr addrName; get_token_addr <- service_addr[addrName]; token_addr = option_bystr20_value get_token_addr;
  match beneficiary with
  | UserDomain username domain =>
      init <- init_;
      get_addr <-& init.dns[domain][username]; addr = option_bystr20_value get_addr;
      accept; msg = let m = { _tag: "Transfer"; _recipient: token_addr; _amount: zero;
        to: addr;
        amount: amount } in one_msg m ; send msg
  | BeneficiaryAddr addr =>
      accept; msg = let m = { _tag: "Transfer"; _recipient: token_addr; _amount: zero;
        to: addr;
        amount: amount } in one_msg m ; send msg end end

transition RecipientAcceptTransfer( sender: ByStr20, recipient: ByStr20, amount: Uint128 ) IsOperational end

transition RecipientAcceptTransferFrom( initiator: ByStr20, sender: ByStr20, recipient: ByStr20, amount: Uint128 ) IsOperational end

transition TransferSuccessCallBack( sender: ByStr20, recipient: ByStr20, amount : Uint128 ) IsOperational end

transition TransferFromSuccessCallBack( initiator: ByStr20, sender: ByStr20, recipient: ByStr20, amount: Uint128 ) IsOperational end

(* The xWallet backbone *)

transition UpdateDeadline( amount: Uint128 )
  IsOperational; VerifyAdmin; Payment update fixed; deadline_block := amount end

procedure IncreaseAllowance(
  addrName: String,
  amount: Uint128
  )
  FetchServiceAddr addrName; get_addr <- service_addr[addrName]; addr = option_bystr20_value get_addr;
  FetchServiceAddr zilswap; get_zilswap <- service_addr[zilswap]; zilswap_addr = option_bystr20_value get_zilswap;
  msg = let m = {
    _tag: "IncreaseAllowance"; _recipient: addr; _amount: zero;
    spender: zilswap_addr;
    amount: amount } in one_msg m ; send msg end

procedure VerifyCaller(
  data: Data,
  signature: Option ByStr64
  )
  current_admin <- admin_;
  is_admin = builtin eq current_admin _sender;
  match is_admin with
  | True =>
  | False =>
      current_agent <- agent;
      is_agent = builtin eq current_agent _sender;
      match is_agent with
      | True =>
          match signature with
          | None => err = CodeNotValid; ThrowError err
          | Some sig =>
              match data with
              | Data id addrName sndAddrName amt sndAmt trdAmt =>
                  signed_data = let h1 = builtin sha256hash addrName
                  in let h2 = builtin sha256hash sndAddrName
                  in let h3 = builtin sha256hash amt
                  in let h4 = builtin sha256hash sndAmt
                  in let h5 = builtin sha256hash trdAmt
                  in let h1_2 = builtin concat h1 h2
                  in let h1__3 = builtin concat h1_2 h3
                  in let h1__4 = builtin concat h1__3 h4
                  in let h1__5 = builtin concat h1__4 h5
                  in builtin to_bystr h1__5;
                  VerifySignature xorder signed_data sig end end
      | False => err = CodeWrongCaller; ThrowError err end end end

transition AddLiquidity(
  data: Data,
  signature: Option ByStr64
  )
  IsOperational; VerifyCaller data signature;
  match data with
  | Data id addrName none amt sndAmt none_ =>
      IncreaseAllowance addrName amt;
      get_zilswap <- service_addr[zilswap]; zilswap_addr = option_bystr20_value get_zilswap;
      get_addr <- service_addr[addrName]; addr = option_bystr20_value get_addr;
      current_block <- &BLOCKNUMBER; deadline <- deadline_block; this_deadline = builtin badd current_block deadline;
      amount = Some{ Uint128 } amt; Payment zilswap amount;
      accept; msg = let m = {
        _tag: "AddLiquidity"; _recipient: zilswap_addr; _amount: _amount;
        token_address: addr;
        min_contribution_amount: amt;
        max_token_amount: sndAmt;
        deadline_block: this_deadline } in one_msg m ; send msg end end

transition RemoveLiquidity(
  data: Data,
  signature: Option ByStr64
  )
  IsOperational; VerifyCaller data signature;
  match data with
  | Data id addrName none_ amt sndAmt trdAmt =>
      FetchServiceAddr zilswap; get_zilswap <- service_addr[zilswap]; zilswap_addr = option_bystr20_value get_zilswap;
      FetchServiceAddr addrName; get_addr <- service_addr[addrName]; addr = option_bystr20_value get_addr;
      current_block <- &BLOCKNUMBER; deadline <- deadline_block; this_deadline = builtin badd current_block deadline;
      amount = Some{ Uint128 } amt; Payment zilswap amount;
      accept; msg = let m = {
        _tag: "RemoveLiquidity"; _recipient: zilswap_addr; _amount: _amount;
        token_address: addr;
        contribution_amount: amt;
        min_zil_amount: sndAmt;
        min_token_amount: trdAmt;
        deadline_block: this_deadline } in one_msg m ; send msg end end

transition SwapExactZILForTokens(
  data: Data,
  signature: Option ByStr64
  )
  IsOperational; VerifyCaller data signature;
  match data with
  | Data id addrName none amt sndAmt none_ =>
    FetchServiceAddr zilswap; get_zilswap <- service_addr[zilswap]; zilswap_addr = option_bystr20_value get_zilswap;
    FetchServiceAddr addrName; get_addr <- service_addr[addrName]; addr = option_bystr20_value get_addr;
    current_block <- &BLOCKNUMBER; deadline <- deadline_block; this_deadline = builtin badd current_block deadline;
    amount = Some{ Uint128 } amt; Payment zilswap amount;
    accept; msg = let m = {
      _tag: "SwapExactZILForTokens"; _recipient: zilswap_addr; _amount: amt;
      token_address: addr;
      min_token_amount: sndAmt;
      deadline_block: this_deadline;
      recipient_address: _this_address } in one_msg m; send msg end end

transition SwapExactTokensForZIL(
  data: Data,
  signature: Option ByStr64
  )
  IsOperational; VerifyCaller data signature; Payment education fixed;
  match data with
  | Data id addrName none amt sndAmt none_ =>
    IncreaseAllowance addrName amt;
    get_zilswap <- service_addr[zilswap]; zilswap_addr = option_bystr20_value get_zilswap;
    get_addr <- service_addr[addrName]; addr = option_bystr20_value get_addr;
    current_block <- &BLOCKNUMBER; deadline <- deadline_block; this_deadline = builtin badd current_block deadline;
    accept; msg = let m = {
      _tag: "SwapExactTokensForZIL"; _recipient: zilswap_addr; _amount: zero;
      token_address: addr;
      token_amount: amt;
      min_zil_amount: sndAmt;
      deadline_block: this_deadline;
      recipient_address: _this_address } in one_msg m ; send msg end end
    
transition SwapExactTokensForTokens(
  data: Data,
  signature: Option ByStr64
  )
  IsOperational; VerifyCaller data signature; Payment education fixed;
  match data with
  | Data id tokenName toTokenName amt sndAmt trdAmt =>
    IncreaseAllowance tokenName amt;
    get_zilswap <- service_addr[zilswap]; zilswap_addr = option_bystr20_value get_zilswap;
    get_addr <- service_addr[tokenName]; addr = option_bystr20_value get_addr;
    FetchServiceAddr toTokenName; get_sndAddr <- service_addr[toTokenName]; sndAddr = option_bystr20_value get_sndAddr;
    current_block <- &BLOCKNUMBER; deadline <- deadline_block; this_deadline = builtin badd current_block deadline;
    accept; msg = let m = {
      _tag: "SwapExactTokensForTokens"; _recipient: zilswap_addr; _amount: zero;
      token0_address: addr;
      token1_address: sndAddr;
      token0_amount: amt;
      min_token1_amount: sndAmt;
      deadline_block: this_deadline;
      recipient_address: _this_address } in one_msg m ; send msg end end

transition DelegateStake(
  data: Data,
  signature: Option ByStr64
  )
  IsOperational; VerifyCaller data signature;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  match data with
  | Data id ssnName sndAddrName amt sndAmt trdAmt =>
      FetchServiceAddr ssnName; get_ssnaddr <- service_addr[ssnName]; ssnaddr = option_bystr20_value get_ssnaddr;
      amount_ = Some{ Uint128 } amt; Payment stakezil amount_;
      current_agent <- agent; is_agent = builtin eq current_agent ssnaddr;
      match is_agent with
      | True =>
      | False => Payment id amount_ end;
      accept; msg = let m = { _tag: "DelegateStake"; _recipient: addr; _amount: amt;
        ssnaddr: ssnaddr } in one_msg m; send msg end end

transition DelegateStakeSuccessCallBack( ssnaddr: ByStr20, amount: Uint128 ) IsOperational end

transition WithdrawStakeRewards(
  data: Data,
  signature: Option ByStr64
  )
  IsOperational; VerifyCaller data signature;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  match data with
  | Data id ssnName none none_ none__ none___ =>
      FetchServiceAddr ssnName; get_ssnaddr <- service_addr[ssnName]; ssnaddr = option_bystr20_value get_ssnaddr;
      Payment education fixed;
      current_agent <- agent; is_agent = builtin eq current_agent ssnaddr;
      match is_agent with
      | True =>
      | False => Payment id fixed end;
      accept; msg = let m = { _tag: "WithdrawStakeRewards"; _recipient: addr; _amount: zero;
        ssnaddr: ssnaddr } in one_msg m; send msg end end

transition WithdrawStakeRewardsSuccessCallBack( ssnaddr: ByStr20, rewards: Uint128 ) IsOperational end  

transition WithdrawStakeAmt(
  ssnName: String,
  amount: Uint128
  )
  IsOperational; VerifyAdmin;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  FetchServiceAddr ssnName; get_ssnaddr <- service_addr[ssnName]; ssnaddr = option_bystr20_value get_ssnaddr;
  amount_ = Some{ Uint128 } amount; Payment stakezil amount_;
  accept; msg = let m = { _tag: "WithdrawStakeAmt"; _recipient: addr; _amount: zero;
    ssnaddr: ssnaddr;
    amt: amount } in one_msg m; send msg end

transition WithdrawStakeAmtSuccessCallBack( ssnaddr: ByStr20, amount: Uint128 ) IsOperational end

transition CompleteWithdrawal()
  IsOperational; VerifyAdmin; Payment education fixed;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  accept; msg = let m = { _tag: "CompleteWithdrawal"; _recipient: addr; _amount: zero } in one_msg m; send msg end

transition CompleteWithdrawalNoUnbondedStakeCallBack( amount: Uint128 ) IsOperational end

transition CompleteWithdrawalSuccessCallBack( amount: Uint128 ) IsOperational end

transition ReDelegateStake(
  ssnName: String,
  toSsnName: String,
  amount: Uint128
  )
  IsOperational; VerifyAdmin;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  FetchServiceAddr ssnName; get_ssnaddr <- service_addr[ssnName]; ssnaddr = option_bystr20_value get_ssnaddr;
  FetchServiceAddr toSsnName; get_to_ssnaddr <- service_addr[toSsnName]; to_ssnaddr = option_bystr20_value get_to_ssnaddr;
  amount_ = Some{ Uint128 } amount; Payment stakezil amount_;
  accept; msg = let m = { _tag: "ReDelegateStake"; _recipient: addr; _amount: zero;
    ssnaddr: ssnaddr;
    to_ssn: to_ssnaddr;
    amount: amount } in one_msg m; send msg end

transition ReDelegateStakeSuccessCallBack( ssnaddr: ByStr20, tossn: ByStr20, amount: Uint128 ) IsOperational end

transition RequestDelegatorSwap( newAddr: ByStr20 )
  IsOperational; VerifyAdmin; Payment update fixed;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  accept; msg = let m = { _tag: "RequestDelegatorSwap"; _recipient: addr; _amount: zero;
    new_deleg_addr: newAddr } in one_msg m; send msg end

transition ConfirmDelegatorSwap( requestor: ByStr20 )
  IsOperational; VerifyAdmin; Payment education fixed;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  accept; msg = let m = { _tag: "ConfirmDelegatorSwap"; _recipient: addr; _amount: zero;
    requestor: requestor } in one_msg m; send msg end

transition RevokeDelegatorSwap()
  IsOperational; VerifyAdmin; Payment education fixed;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  accept; msg = let m = { _tag: "RevokeDelegatorSwap"; _recipient: addr; _amount: zero } in one_msg m; send msg end

transition RejectDelegatorSwap( requestor: ByStr20 )
  IsOperational; VerifyAdmin; Payment education fixed;
  FetchServiceAddr stakezil; get_addr <- service_addr[stakezil]; addr = option_bystr20_value get_addr;
  accept; msg = let m = { _tag: "RejectDelegatorSwap"; _recipient: addr; _amount: zero;
    requestor: requestor } in one_msg m; send msg end